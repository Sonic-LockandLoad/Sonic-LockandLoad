class DeathInfo : Object {
	Actor actor;
	int tick;
}

class RemoveCyberSpaceArenaCorpses : EventHandler
{
	Array<DeathInfo> dead_monsters;
	int current_tick;
	int delay;

	override void WorldThingDied(WorldEvent e)
	{
		if (e.thing && e.thing.bIsMonster && level.mapname == "SDCYBRSP") {
			DeathInfo info = new("DeathInfo");
			info.actor = e.thing;
			info.tick = current_tick;
			dead_monsters.Push(info);
		}
	}

	override void WorldTick () {
		current_tick++;
		delay = 60;
		for (int i = 0; i < dead_monsters.Size(); i++) {
			if (current_tick - dead_monsters[i].tick >= delay) {
				dead_monsters[i].actor.Destroy();
				dead_monsters.Delete(i);
				i--;
			}
		}
	}
}

class GiveEnergyOnDamage : EventHandler
{
	void GiveEnergy(Actor recipient, int energy, int maxenergy) {
		if (recipient && recipient.CountInv("ChaosEnergy") < maxenergy) {
			if (recipient.CountInv("ChaosEnergy") + energy > maxenergy) {
				energy = maxenergy - recipient.CountInv("ChaosEnergy");
			}
			recipient.A_GiveInventory("ChaosEnergy", energy);
		}
	}

	override void WorldThingDamaged(WorldEvent e)
	{
		if (e.thing && e.thing.bIsMonster && Cvar.FindCvar("GiveEnergyOnDamage").GetBool()) {
			int energy_to_give, min_energy, max_energy;
			if (e.damage > e.thing.health) {
				energy_to_give = e.damage / 16;
				min_energy = 1;
				max_energy = 300;
			}
			else {
				energy_to_give = e.damage / 8;
				min_energy = 15;
				max_energy = 500;
			}
			if (e.damage < min_energy) {
				energy_to_give = min_energy;
			}
			GiveEnergy(e.damagesource, energy_to_give, max_energy);
		}
	}
}

class EnemiesBleedRings : EventHandler
{
	override void WorldThingDamaged(WorldEvent e)
	{
		if (e.thing && e.thing.bIsMonster && Cvar.FindCvar("EnemiesBleedRings").GetBool()) {
			int ringcount = e.damage / 64;
			ringcount = clamp(ringcount, 1, 300);
			int maxrange  = e.damage / 48;
			maxrange = clamp(maxrange, 2, 20);
			for (int x = 1; x <= ringcount; x++) {
				int x = random(maxrange * -1, maxrange);
				int y = random(maxrange * -1, maxrange);
				bool ring_exists;
				Actor spawned_ring;
				[ring_exists, spawned_ring] = e.thing.A_SpawnItemEx("DroppedRingEnemy", 0, 0, 1, x, y, 12, 0, SXF_NOCHECKPOSITION);
				if (ring_exists) {
					spawned_ring.Vel += e.thing.Vel / 2;
				}
			}
			FTranslatedLineTarget t;
			for (int offset = 0; offset < 360; offset += 45) {
				e.thing.LineAttack(e.thing.angle + offset, 192, e.thing.pitch, 0, "None", "InvisiblePuff", 0, t);
				if (t.linetarget && t.linetarget is "PlayerPawn") {
					t.linetarget.A_GiveInventory("Health", ringcount / 4);
				}
			}
		}
	}
}

class StyleSystem : EventHandler
{
	int score;
	int deprecation_tick;
	int reward_tick;
	string current_weapon;
	int consecutive_uses;

	override void WorldThingDamaged(WorldEvent e)
	{
		bool is_attackable_target = e.thing.bIsMonster || e.thing.GetClassName() == "Doox";

		if (!cvar.FindCvar("StyleSystem").GetBool())
			return;

		if (e.thing && e.thing is "PlayerPawn") {
			DeductStyleScore(e);
		}

		else if (e.thing && is_attackable_target && !(e.damagesource && e.damagesource.bIsMonster) && !(e.inflictor && e.inflictor.bIsMonster)) {
			UpdateStyleScore(e);
		}
	}

	override void WorldTick()
	{
		if (!cvar.FindCvar("StyleSystem").GetBool())
			return;

		UpdateStyleRank();
		UpdateWeapon();
		DeprecateStyleScore();
	}

	private void UpdateStyleScore(WorldEvent e)
	{
		/* Player object */
		PlayerPawn player = players[consoleplayer].mo;

		/* Consecutive uses */
		let ReadyWeapon = player.player.ReadyWeapon;
		let PlayerIsResponsible = e.damagesource && e.damagesource is "PlayerPawn";
		if (ReadyWeapon && ReadyWeapon.GetClassName() == current_weapon && PlayerIsResponsible) {
			consecutive_uses++;
		}

		/* Initialisation */
		int stylerank = cvar.FindCvar("StyleRank").GetInt();
		int scorebonus;

		/* Dealing damage */
		if (PlayerIsResponsible) {
			scorebonus += e.damage * 73;
		}
		else {
			if (stylerank >= 1) {
				scorebonus += e.damage * 12;
			}
			else {
				return;
			}
		}

		/* Using different weapon */
		if (ReadyWeapon.GetClassName() != current_weapon) {
			scorebonus *= 2;
		}

		/* Height bonus */
		if (!player.player.onground) {
			scorebonus *= 1.3;
		}

		/* Enemy mid-air bonus */
		if (e.thing.Pos.Z > e.thing.GetZAt()) {
			scorebonus *= 2;
		}

		/* Add case for Offhand Attack */
		bool offhand_attack_used = e.DamageType == "SonicKick" ||
								   e.DamageType == "ShadowMelee" ||
								   e.DamageType == "Piko" ||
								   e.DamageType == "SonicPunch";

		/* Add case for shotguns */
		bool shotgun_used = current_weapon == "KineticShotty" ||
							current_weapon == "PrecisionShotgun" ||
							current_weapon == "SpeedyShotgun" ||
							current_weapon == "DarkAssault" ||
							current_weapon == "QuadShotty";

		/* Clamp score bonus */
		int minimum_score = 750 + GetScoreBonusCap() / 100;
		int maximum_score = GetScoreBonusCap();
		scorebonus = clamp(scorebonus, minimum_score, maximum_score);

		/* Limit score bonus if attack was Spin Dash or Boost */
		if (e.damagetype == "SpinDash" || e.DamageType == "Boost") {
			scorebonus /= 8;
		}

		/* Calculate staleness */
		int staleness_factor = 224;
		if (shotgun_used) staleness_factor = 96;
		int staleness = staleness_factor * consecutive_uses;
		staleness = clamp(staleness, 0, scorebonus);

		// Apply staleness, unless an Offhand Attack was used
		if (!offhand_attack_used) {
			scorebonus -= staleness;
		}

		/* Total up and cap score */
		score += scorebonus;
		if (score > 300000) score = 300000;

		/* It isn't here, but the player can also gain Style
		   Points by parrying attacks.

		   Go and see Sonic.zsc and Amy.zsc for that code. */
	}

	private void DeductStyleScore(WorldEvent e)
	{
		int penalty;
		int stylerank = cvar.FindCvar("StyleRank").GetInt();
		switch (stylerank) {
			case 0: // E
				penalty = 0;
				break;
			case 1: // D
				penalty = 5000;
				break;
			case 2: // C
				penalty = 5000;
				break;
			case 3: // B
				penalty = 15000;
				break;
			case 4: // A
				penalty = 30000;
				break;
			case 5: // S
				penalty = 40000;
				break;
			case 6: // SS
				penalty = 50000;
				break;
			case 7: // SSS
				penalty = 60000;
				break;
			case 8: // U
                penalty = 70000;
                break;
		}
		score -= penalty;
	}

	private int GetScoreBonusCap()
	{
		PlayerPawn player = players[consoleplayer].mo;
		int stylerank = cvar.FindCvar("StyleRank").GetInt();
		int cap;
		switch (stylerank) {
			case 0: // E
			case 1: // D
			case 2: // C
				cap = 15000;
				break;
			case 3: // B
			case 4: // A
				cap = 10000;
				break;
			case 5: // S
			case 6: // SS
			case 7: // SSS
			case 8: // U
				cap = 5000;
				break;
		}

		return cap;
	}

	private void DeprecateStyleScore()
	{
		PlayerPawn player = players[consoleplayer].mo;
		int stylerank = cvar.FindCvar("StyleRank").GetInt();
		int deprecation;
		switch (stylerank) {
			case 0: // E
			case 1: // D
				deprecation = 32;
				break;
			case 2: // C
			case 3: // B
				deprecation = 128;
				break;
			case 4: // A
			case 5: // S
				deprecation = 256;
				break;
			case 6: // SS
			case 7: // SSS
			case 8: // U
				deprecation = 512;
				break;
		}

		/* Super and Hyper forms are invincible,
		   so this is to balance it out */
		if (player.CountInv("PowerSuper") || player.CountInv("PowerHyper")) {
			deprecation *= 1.5;
		}

		/* Deducting score */
		if (deprecation_tick > 2) {
			deprecation_tick = 0;
			score -= deprecation;
		}
	}

	private void UpdateStyleRank()
	{
		PlayerPawn player = players[consoleplayer].mo;
		if (score < 0 || player.health <= 0) score = 0;
		cvar.FindCvar("StyleScore").SetInt(score);

		deprecation_tick++;
		reward_tick++;

		// Set your Style Rank
		if 		(score >= 280000) cvar.FindCvar("StyleRank").SetInt(8); // U
		else if	(score >= 210000) cvar.FindCvar("StyleRank").SetInt(7); // SSS
		else if (score >= 150000) cvar.FindCvar("StyleRank").SetInt(6); // SS
		else if (score >= 100000) cvar.FindCvar("StyleRank").SetInt(5); // S
		else if (score >= 60000)  cvar.FindCvar("StyleRank").SetInt(4); // A
		else if (score >= 30000)  cvar.FindCvar("StyleRank").SetInt(3); // B
		else if (score >= 15000)  cvar.FindCvar("StyleRank").SetInt(2); // C
		else if (score >= 10000)  cvar.FindCvar("StyleRank").SetInt(1); // D
		else 					  cvar.FindCvar("StyleRank").SetInt(0); // E

		// Obtain Style Rank to sync up
		int style_rank = cvar.FindCvar("StyleRank").GetInt();

		if (style_rank >= 3) {
			GiveStyleRewards(player, style_rank);
		}
	}

	private void GiveStyleRewards(Actor player, int style_rank)
	{
		bool is_transformed = player.CountInv("PowerSuper") || player.CountInv("PowerHyper");
		bool rewards_enabled = cvar.FindCvar("StyleBonuses").GetBool();

		if (is_transformed && rewards_enabled) {
			return;
		}

		int delay;
        int multiplier;

		switch (style_rank) {
			case 8:
			case 7:
				delay = 1;
                multiplier = 2;
				break;
			case 6:
				delay = 1;
                multiplier = 1;
				break;
			case 5:
				delay = 2;
                multiplier = 2;
				break;
			case 4:
				delay = 2;
                multiplier = 1;
				break;
			case 3:
			default:
				delay = 3;
                multiplier = 1;
				break;
		}

		if (reward_tick % delay == 0) {
			player.A_GiveInventory("ChaosEnergy", multiplier);
		}
	}

	private void UpdateWeapon()
	{
		PlayerPawn player = players[consoleplayer].mo;

		/* If the player's weapon has previously been
		   switched, reset the amount of consecutive
		   uses and reset their current weapon. */
		if (player.player.ReadyWeapon && player.player.ReadyWeapon.GetClassName() != current_weapon) {
			consecutive_uses = 0;
			current_weapon = player.player.ReadyWeapon.GetClassName();
		}
	}
}

class SpongyEnemies : EventHandler
{
	override void WorldThingSpawned(WorldEvent e)
	{
		if (e.thing && e.thing.bIsMonster && Cvar.FindCvar("SpongyEnemies").GetBool()) {
			e.thing.health *= cvar.FindCvar("Sponginess").GetInt();
			// It really is that fricking simple! :D
		}
	}
}

class DeathCounter : StaticEventHandler
{
	override void PlayerSpawned(PlayerEvent e)
	{
		int counter = cvar.FindCvar("DeathCounter").GetInt();
		if (counter > 0) {
			CallACS("DisplayFailures", counter);
		}
	}
	override void PlayerDied(PlayerEvent e)
	{
		int counter = cvar.FindCvar("DeathCounter").GetInt();
		if (cvar.FindCvar("CountDeaths").GetBool()) {
			cvar.FindCvar("DeathCounter").SetInt(counter + 1);
		}
	}
}

class LastWeaponHandler : StaticEventHandler
{
	string last_weapon;
	string current_weapon;

	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name == "quickswap") {
			SwitchWeapon();
		}
	}

	string GetDefaultWeapon()
	{
		PlayerPawn player = players[consoleplayer].mo;

		// Can't really use switch-case here, since we're working with strings

		// We can assume that the player just spawned in, so their weapons are
		// easy enough to guess

		if (player is "SonicPlayer") {
			return "Caliburn";
		}

		if (player is "AmyPlayer") {
			return "SilentRosePistol";
		}

		if (player is "ClassicSonicPlayer") {
			return "ChaosBlaster";
		}

		if (player is "ShadowPlayer") {
			return "HighShovel";
		}

		return "";
	}

	string GetWeapon()
	{
		PlayerPawn player = players[consoleplayer].mo;

		if (player.player.ReadyWeapon == null) {
			return "";
		}

		return player.player.ReadyWeapon.GetClassName();
	}

	override void WorldTick()
	{
		PlayerPawn player = players[consoleplayer].mo;

		if (player.player.ReadyWeapon == null) {
			return;
		}

		if (current_weapon != GetWeapon()) {
			last_weapon = current_weapon;
		}

		if (last_weapon == "") {
			last_weapon = GetDefaultWeapon();
		}

		current_weapon = GetWeapon();
	}

	void SwitchWeapon()
	{
		PlayerPawn player = players[consoleplayer].mo;

		if (!player.CountInv(last_weapon)) {
			last_weapon = GetDefaultWeapon();
		}

		player.A_SelectWeapon(last_weapon);
	}
}

class CheckpointRemovalHandler : EventHandler
{
	override void WorldThingSpawned(WorldEvent e)
	{
		bool checkpoints_disabled = !cvar.FindCvar("CheckpointsEnabled").GetBool();
		bool permadeath_enabled = cvar.FindCvar("PermadeathMode").GetBool();
		bool is_checkpoint = e.thing.GetClassName() == "sp_SaveDisk";

		bool checkpoints_should_spawn = checkpoints_disabled || permadeath_enabled;

		if (checkpoints_should_spawn && is_checkpoint) {
			e.thing.Destroy();
		}
	}
}
